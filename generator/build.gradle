plugins {
    id 'org.sonarqube' version '2.6.2'
    id 'java'
    id 'maven'
    id 'signing'
    id 'jacoco'
}

group = 'net.codecrete.qrbill'
version = '1.0.0'
archivesBaseName = 'qrbill-generator'

sourceCompatibility = 1.8

jar {
}

test {
    useJUnitPlatform()
}

javadoc {
    include 'net/codecrete/qrbill/canvas/*'
    include 'net/codecrete/qrbill/generator/*'

    title = 'QR Bill Generator ${version}'
    options.addBooleanOption('html5', true)
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Swiss QR Bill'
                packaging 'jar'
                description 'Java library for generating Swiss QR bills'
                url 'https://github.com/manuelbl/SwissQRBill'

                scm {
                    connection 'scm:git:git://github.com/manuelbl/SwissQRBill.git'
                    developerConnection 'scm:git:ssh://github.com:manuelbl/SwissQRBill.git'
                    url 'https://github.com/manuelbl/SwissQRBill/tree/master'
                }

                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'manuelbl'
                        name 'Manuel Bleichenbacher'
                        email 'manuel.bleichenbacher@gmail.com'
                    }
                }
            }
        }
    }
}

sonarqube {
    properties {
        property 'sonar.exclusions', 'src/main/java/net/codecrete/io/nayuki/qrcodegen/*.java'
    }
}

dependencies {
    implementation 'org.apache.pdfbox:pdfbox:2.0.4'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.2.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.2.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
}
