apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

group = 'net.codecrete.qrbill'
version = '0.9.1'
archivesBaseName = "qrbill-generator"

jar {
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
    exclude '**/nayuki/**'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Swiss QR Bill'
                packaging 'jar'
                description 'Java library for generating Swiss QR bills.'
                url 'https://github.com/manuelbl/SwissQRBill'

                scm {
                    connection 'scm:git:git://github.com/manuelbl/SwissQRBill.git'
                    developerConnection 'scm:git:ssh://github.com:manuelbl/SwissQRBill.git'
                    url 'https://github.com/manuelbl/SwissQRBill/tree/master'
                }

                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'manuelbl'
                        name 'Manuel Bleichenbacher'
                        email 'manuel.bleichenbacher@gmail.com'
                    }
                }
            }
        }
    }
}

dependencies {
    compile group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.4'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
