buildscript {
    repositories {
        mavenCentral()
    }
}
    
plugins {
    id 'org.sonarqube' version '3.0'
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
}

repositories {
    mavenCentral()
}

group = 'net.codecrete.qrbill'
version = '2.5.4'
archivesBaseName = 'qrbill-generator'

sourceCompatibility = 1.8

jar {
    manifest {
        attributes(
            'Implementation-Title': 'Swiss QR Bill Generator',
            'Implementation-Version': archiveVersion,
            'Implementation-Vendor': 'Manuel Bleichenbacher'
        )

    }
}

test {
    useJUnitPlatform()
}

javadoc {
    include 'net/codecrete/qrbill/canvas/*'
    include 'net/codecrete/qrbill/generator/*'

    title = "QR Bill Generator ${version}"
    options.addBooleanOption('html5', true)
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'qrbill-generator'
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Swiss QR Bill'
                description = 'Java library for generating Swiss QR bills'
                url = 'https://github.com/manuelbl/SwissQRBill'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'manuelbl'
                        name = 'Manuel Bleichenbacher'
                        email = 'manuel.bleichenbacher@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/manuelbl/SwissQRBill.git'
                    developerConnection = 'scm:git:ssh://github.com:manuelbl/SwissQRBill.git'
                    url = 'https://github.com/manuelbl/SwissQRBill/tree/master'
                }
            }
        }
    }
    repositories {
        maven {
            def releaseRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releaseRepoUrl
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

sonarqube {
    properties {
        property 'sonar.exclusions', 'src/main/java/net/codecrete/io/nayuki/qrcodegen/*.java'
        property 'sonar.projectName', 'Swiss QR Bill Generator (Java)'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled false
    }
}

project.tasks["sonarqube"].dependsOn "jacocoTestReport"
project.tasks["publishMavenJavaPublicationToMavenLocal"].dependsOn "signArchives"
project.tasks["publishMavenJavaPublicationToMavenRepository"].dependsOn "signArchives"

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    implementation 'io.nayuki:qrcodegen:[1.6.0,1.999999]'
    implementation 'org.apache.pdfbox:pdfbox:[2,2.999999]'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}
