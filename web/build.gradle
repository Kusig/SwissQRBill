buildscript {
    dependencies {
        classpath "org.openapitools:openapi-generator-gradle-plugin:3.3.2"
    }
}

plugins {
    id 'org.sonarqube' version '2.6.2'
    id 'java'
    id 'org.springframework.boot' version '2.1.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'jacoco'
}

apply plugin: 'org.openapi.generator'

group = 'net.codecrete.qrbill'
version = '2.0.0.RC1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

bootJar {
    baseName = 'qrbill-service'
    mainClassName = 'net.codecrete.qrbill.web.Application'
}

test {
    useJUnitPlatform()
}

def openapiInput = new File(projectDir, '/src/main/resources/static/qrbill.yaml')
def openapiOutputDir = new File(buildDir, 'openapi')
def openapiSourceDir = new File(openapiOutputDir, 'src/main/java')
def openapiIgnoreFiles = new File(projectDir, '.openapi-generator-ignore')

openApiGenerate {
    generatorName = "spring"
    inputSpec = openapiInput.path
    outputDir = openapiOutputDir.path
    apiPackage     = "net.codecrete.qrbill.web.api"
    invokerPackage = "net.codecrete.qrbill.web.config"
    modelPackage   = "net.codecrete.qrbill.web.model"
    ignoreFileOverride = openapiIgnoreFiles.path
    configOptions = [
            java8: true,
            dateLibrary: "java8",
            configPackage: "net.codecrete.qrbill.web.config"
    ]
}

sourceSets {
    main {
        java {
            srcDirs openapiSourceDir
        }
    }
}

dependencies {
    implementation project(':generator')
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'javax.validation:validation-api'
    implementation 'javax.xml.bind:jaxb-api:2.3.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.2.0'
    testImplementation 'com.squareup.okhttp3:okhttp:3.11.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
}

compileJava.dependsOn tasks.openApiGenerate

sonarqube {
}
